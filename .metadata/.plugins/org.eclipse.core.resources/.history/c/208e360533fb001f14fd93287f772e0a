/*
 * generated by Xtext 2.38.0
 */
package org.xtext.example.mydsl.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import org.xtext.example.mydsl.pMDSL.Entity

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class PMDSLGenerator extends AbstractGenerator {
	
	AbstractGenerator generator = new EcommerceGenerator()
	
	new() {
	}

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		fsa.generateFile("/candy-shop/src/models.ts", resource.allContents.toIterable.filter(Entity).models)
		
		for (e : resource.allContents.toIterable.filter(Entity)) {
			fsa.generateFile("/candy-shop/src/app/api/" + e.name.toFirstLower + "/route.ts", e.apiRoutes)
			fsa.generateFile("/candy-shop/src/components/" + e.name.toFirstLower + ".tsx", e.components)
		}
	}
	
	def models(Iterable<Entity> entities)'''
	
	«FOR entity : entities»
	export interface «entity.name» {
		«FOR attribute: entity.attributes»
		«IF attribute.type.primitive !== null »
		«attribute.name»: «attribute.type.primitive»;
		«ELSEIF attribute.type.entity !== null»
		«attribute.name»: «attribute.type.entity.name»;
		«ELSE»
		«attribute.name»: any;
		«ENDIF»
		«ENDFOR»
	}
	«ENDFOR»

	'''
	
	def apiRoutes(Entity entity)'''
    import { NextResponse } from "next/server"
    import { «entity.name» } from "@/models"

    const mockData: «entity.name»[] = [];

    export async function GET() {
        return NextResponse.json(mockData, { status: 200 });
    }

    export async function POST(req: Request) {
        const newItem: «entity.name» = await req.json();
        mockData.push(newItem);
        return NextResponse.json(newItem, { status: 201 });
    }
	'''
	
	def components(Entity entity)'''
	import { FC } from 'react'
	import { «entity.name» } from "@/models"
	
	const «entity.name»Component: FC<«entity.name»> = ({ «FOR attribute : entity.attributes SEPARATOR ', '»«attribute.name»«ENDFOR» }) => {
	  return (
	      <div>
	        «FOR attribute : entity.attributes»
	        «IF attribute.type.entity !== null» 
	          <div>
	            <strong>«attribute.name»:</strong>
	            <«attribute.name.toFirstUpper»Component «FOR subAttr : attribute.type.entity.attributes»«subAttr.name»={«attribute.name».«subAttr.name»}«ENDFOR» />
	            <div>
	              «FOR subAttr : attribute.type.entity.attributes»
	              <div>{«attribute.name».«subAttr.name»}</div>
	              «ENDFOR»
	            </div>
	          </div>
	        «ELSE»
	          <div>
	            <strong>«attribute.name»:</strong> {«attribute.name»}
	          </div>
	        «ENDIF»
	        «ENDFOR»
	      </div>
	    )
	}

	export default «entity.name»Component
	'''
}
